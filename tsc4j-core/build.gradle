//
// build.gradle
//

plugins {
  id "com.gorylenko.gradle-git-properties"
}

description = "tsc4j CORE implementation"

dependencies {
  api                     project(":tsc4j-api")
  api                     "org.slf4j:slf4j-api"

  testImplementation      "org.slf4j:jcl-over-slf4j"
  testImplementation      "ch.qos.logback:logback-classic"
  testImplementation      "com.github.tomakehurst:wiremock"
  testImplementation      "de.mkammerer.wiremock-junit5:wiremock-junit5"
}

configurations {
  testOutput.extendsFrom (testImplementation)
}

compileTestJava {
  sourceCompatibility = 11
  targetCompatibility = 11
}

task testJar (type: Jar, dependsOn: testClasses) {
  from sourceSets.test.output
  archiveClassifier = 'test'
}

artifacts {
  testOutput testJar
}

gitProperties {
  dateFormat          = "yyyy-MM-dd' 'HH:mm:ssZ"
  dateFormatTimeZone  = "UTC"
  gitPropertiesDir    = "${project.buildDir}/resources/main/com/github/tsc4j/core"

  keys                = [ 'git.build.version', 'git.commit.id', 'git.commit.id.abbrev','git.commit.time' ]
}

description = "Core libraries and implementations"
publishing.publications.mavenJava.pom.description = rootProject.name + ' :: ' + description

// vim:shiftwidth=2 softtabstop=2 expandtab
// EOF
